buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:0.4.0'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta3'
        classpath 'commons-httpclient:commons-httpclient:3.1'
        classpath 'org.ajoberstar:gradle-git:1.1.0'
        classpath 'io.ratpack:ratpack-groovy:1.2.0'
    }
}


apply plugin:'base'

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'org.asciidoctor.convert'

dependencies {
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'
}


task allAsciidoctor {
    subprojects{ sub ->
        pluginManager.withPlugin("org.asciidoctor.convert"){
            dependsOn sub.tasks.asciidoctor
        }
    }
}

task pages(type:Copy) {
    into "gh-pages"
    dependsOn tasks.asciidoctor

    from "build/asciidoctor/html5"

    subprojects{ sub ->
        pluginManager.withPlugin("org.asciidoctor.convert"){
            from(sub.tasks.asciidoctor){
                into sub.name
            }
        }
    }
}

asciidoctorj.noDefaultRepositories = true

asciidoctor {
    backends = ['html5'] // 'pdf' can be produced too

    outputDir = file("build/asciidoctor")
    attributes = ['source-highlighter': 'coderay', toc: '', toclevels: 3, idprefix: '', idseparator: '-', imagesdir:"../img"]
}

clean {
    delete "gh-pages"
}
